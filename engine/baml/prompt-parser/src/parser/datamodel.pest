entry = {
    SOI ~ segment* ~ EOI
}

segment = { empty_lines | code_block | comment_block | dangling_code_block | dangling_comment_block | prompt_text }

code_block    = { BLOCK_OPEN ~ (print_block | input_block | single_arg) ~ BLOCK_CLOSE }
input_block   = { input_keyword ~ ("." ~ identifier)* }
print_block   = { print_keyword ~ template_args? ~ arg_list }
template_args = { "<" ~ identifier ~ ">" }
print_keyword = { "print" ~ identifier }
arg_list      = { "(" ~ (single_arg ~ ("," ~ single_arg)*)? ~ ")" }
single_arg    = { identifier ~ ("." ~ identifier)* }

input_keyword = { "input" }
identifier    = { (ASCII_ALPHA | "_")+ }

comment_text_2 = { (!"///}" ~ ANY)* }
comment_text_1 = { (!"//}" ~ ANY)* }
comment        = {
    ("{///" ~ comment_text_2 ~ "///}")
  | ("{//" ~ comment_text_1 ~ "//}")
}

BLOCK_OPEN  = _{ "{#" }
BLOCK_CLOSE = _{ "}" }

dangling_code_block    = { BLOCK_OPEN ~ (!BLOCK_OPEN ~ !"{//" ~ !"{///" ~ !NEWLINE ~ ANY)+ }
dangling_comment_block = { ("{//" | "{///") ~ (!"//}" ~ !"///}" ~ ANY)+ }

comment_block = { comment }

WHITESPACE  = _{ " " | "\t" }
NEWLINE     = _{ "\n" | "\r\n" | "\r" }
empty_lines = @{ (WHITESPACE* ~ NEWLINE)+ }
prompt_text =  { (!"{#" ~ !"{//" ~ !"{///" ~ ANY)+ }
